WHITESPACE = _{ (" " | NEWLINE ) }

num = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
    ~ !(ASCII_ALPHA)
}

date = @{
    ASCII_DIGIT{4} 
    ~ "-"
    ~ (("0" ~ ('1'..'9')) | ("1" ~ ('0'..'2')))
    ~ "-"
    ~ ((('0'..'1') ~ ('1'..'9')) | ("2" ~ ('0'..'9') | ("3" ~ ('0'..'1'))))
}

time = @{
    (('0'..'1'){0,1} ~ ('0'..'9'){1} | "2"{1} ~ ('0'..'3'){1})
    ~ ":"
    ~ ('0'..'5') ~ ('0'..'9')
    ~ ":"
    ~ ('0'..'5') ~ ('0'..'9')
}

datetime = @{date ~ "T" ~ time}

operator = @{"+" | "-" | "*" | "/" | "%" | "^" | "==" | "!=" | ">=" | "<=" | "<" | ">" }

symbol = @{ 
    ASCII_ALPHA+
    ~ (ASCII_ALPHA | ASCII_DIGIT | "_")*
}

list = { "(" ~ expr* ~ ")" }

expr = { datetime | date | time | num | symbol | operator | list }

risp = { SOI ~ expr* ~ EOI }